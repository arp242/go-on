#!/usr/bin/env zsh
[ "${ZSH_VERSION:-}" = "" ] && echo >&2 "Only works with zsh" && exit 1
setopt err_exit no_unset pipefail

ssh_port=9923

start-machine() {
	local toplevel=${1:-0}
	local arch=x86_64
	local version=10
	local img=$root/disks/$machine-$version.qcow2
	local pid=$root/run/$machine-$version.pid
	local ssh_ip=$(resolve-host $ssh_host)
	local efi=/usr/share/qemu/edk2-x86_64-code.fd
	local efi_vars=$root/disks/windows-10-OVMF_VARS.fd

	has-qemu $arch || return 1

	mkdir -p $root/run
	if has-pid $pid; then
		print "$progname: $machine already started; nothing to do"
		return 0
	fi

	# No display unless this script is called directly.
	local display=(-display 'none')
	(( $toplevel )) && display=(
		-display  'gtk,grab-on-hover=off,zoom-to-fit=off'
		-device   'VGA,vgamem_mb=64'
		# Prevents the mouse from being grabbed when clicking in the guest; QEMU
		# will be able to report the mouse position without having to grab it.
		# Also overrides PS/2 mouse emulation when activated.
		-device   'usb-tablet,bus=input.0'
	)

	print "$progname: starting QEMU VM from $img:t"
	local cmd=(qemu-system-$arch
		-enable-kvm
		-machine      'q35,smm=off,vmport=off'
		-rtc          'base=localtime,clock=host,driftfix=slew'
		-cpu          'host,kvm=on,+hypervisor,+invtsc,l3-cache=on,migratable=no,hv_frequencies,kvm_pv_unhalt,hv_reenlightenment,hv_relaxed,hv_spinlocks=8191,hv_stimer,hv_synic,hv_time,hv_vapic,hv_vendor_id=1234567890ab,hv_vpindex'
		-smp          'cores=1,threads=2,sockets=1'
		-m            '4G'
		-name         "$machine,process=goon-$machine"
		-pidfile      "$pid"
		-device       'usb-ehci,id=input'

		# Hard drive
		-device       'virtio-blk-pci,drive=SystemDisk'
		-drive        "id=SystemDisk,if=none,format=qcow2,file=$img"

		# Network
		-device       'virtio-net,netdev=nic'
		-netdev       "user,hostname=windows-10,hostfwd=tcp:$ssh_ip:$ssh_port-:22,id=nic"

		$display

		# EFI
		-global       'driver=cfi.pflash01,property=secure,value=on'
		-drive        "if=pflash,format=raw,unit=0,file=$efi,readonly=on"
		-drive        "if=pflash,format=raw,unit=1,file=$efi_vars"

		# Random stuff from quickemu; not sure what's needed, but keep it for now.
		-no-hpet
		-global       kvm-pit.lost_tick_policy=discard
		-global       ICH9-LPC.disable_s3=1
		-device       virtio-balloon
		-smbios       type=2,manufacturer="QuickemuProject",product=Quickemu,version=3.16,serial=0xDEADBEEF,location=quickemu.com,asset=windows-10
		-device       virtio-serial-pci
		-device       virtio-rng-pci,rng=rng0
		-object       rng-random,id=rng0,filename=/dev/urandom
		-device       pci-ohci,id=smartpass
		-device       usb-kbd,bus=input.0
	)
	${cmd[@]} &
}

if [[ $zsh_eval_context = 'toplevel' ]]; then
	source $0:P:h:h/goon
	read-machine $0:P:t
	start-machine 1
fi
